// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
                                                 // curr <
    Mux16(a=currReg, b=incReg, out=s1, sel=inc); // inc <
    Mux16(a=s1, b=in, out=s2, sel=load);         // load <
    Mux16(a=s2, b=false, out=newReg, sel=reset); // reset

    Inc16(in=currReg, out=incReg);

    Register(in=newReg, load=true, out=out, out=currReg);
}
